Mailbox         <-   LocalPart '@' Domain ;

Domain          <-   DomainName / AddressLiteral ;

DomainName      <-   SubDomain ('.' SubDomain)* ;

SubDomain       <-   LetDig LdhStr? ;

LetDig          <-   ALPHA / DIGIT ;

HyphenLetDig    <-   "-" LetDig ;

LdhStr          <-   (HyphenLetDig / LetDig)+ ;

AddressLiteral  <-   '[' ( IPv4AddressLiteral / IPv6AddressLiteral / GeneralAddressLiteral ) ']' ;

LocalPart       <-   DotString / QuotedString ;

DotString       <-   Atom ('.'  Atom)* ;

QuotedString    <-   DQUOTE QcontentSMTP* DQUOTE ;

QcontentSMTP    <-   QtextSMTP / QuotedPairSMTP ;

QuotedPairSMTP  <-   [\\x5C] [\\x20-\\x7E] ;

QtextSMTP       <-   [\\x20-\\x21] /
                     [\\x23-\\x5B] /
                     [\\x5D-\\x7E] /
                     UTF8NonASCII ;

Atext           <-   ALPHA / DIGIT /
                     '!' / '#' /
                     '$' / '%' /
                     '&' / "'" /
                     '*' / '+' /
                     '-' / '/' /
                     '=' / '?' /
                     '^' / '_' /
                     '`' / '{' /
                     '|' / '}' /
                     '~' / UTF8NonASCII ;

Atom            <-   Atext+ ;

UTF8NonASCII    <-   UTF8TwoByte / UTF8ThreeByte / UTF8FourByte ;

UTF8TwoByte     <-   [\\xC2-\\xDF] UTF8Tail ;

UTF8ThreeByte   <-   [\\xE0] [\\xA0-\\xBF] UTF8Tail /
                     [\\xE1-\\xEC] UTF8Tail UTF8Tail /
                     [\\xED] [\\x80-\\x9F] UTF8Tail /
                     [\\xEE-\\xEF] UTF8Tail UTF8Tail ;

UTF8FourByte    <-   [\\xF0] [\\x90-\\xBF] UTF8Tail UTF8Tail /
                     [\\xF1-\\xF3] UTF8Tail UTF8Tail UTF8Tail /
                     [\\xF4] [\\x80-\\x8F] UTF8Tail UTF8Tail ;

UTF8Tail       <-   [\\x80-\\xBF] ;

IPv4AddressLiteral     <-   Snum ("."  Snum) ("."  Snum) ("."  Snum) ;

IPv6AddressLiteral     <-   "IPv6:" IPv6Addr ;

GeneralAddressLiteral  <-   StandardizedTag ':' DContent+ ;

StandardizedTag        <-   LdhStr ;

DContent        <-   [\\x33-\\x5A] / [\\x5E-\\x7E] ;

IPv6Addr        <-  IPv6Full /
                    IPv6Comp /
                    IPv6v4Full /
                    IPv6v4Comp ;

IPv6Hex         <- HEXDIG HEXDIG? HEXDIG? HEXDIG? ;

IPv6Full        <- IPv6Hex (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) ;

IPv6Comp        <- (IPv6Hex (":" IPv6Hex) (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)?)? "::"
                    (IPv6Hex (":" IPv6Hex) (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)?)? ;

IPv6v4Full      <- IPv6Hex (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) (":" IPv6Hex) ":" IPv4AddressLiteral ;

IPv6v4Comp      <- (IPv6Hex (":" IPv6Hex) (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)?)? "::"
                    (IPv6Hex (":" IPv6Hex) (":" IPv6Hex)? (":" IPv6Hex)? (":" IPv6Hex)? ":")?
                    IPv4AddressLiteral ;

Snum            <-   DIGIT DIGIT? DIGIT? ;

LF              <-   [\\x0A] ;

CR              <-   [\\x0D] ;

ALPHA           <-   [\\x41-\\x5A] / [\\x61-\\x7A] ;

DIGIT           <-   [\\x30-\\x39] ;

SP              <-   [\\x20] ;

HTAB            <-   [\\x09] ;

WSP             <-   SP / HTAB ;

CRLF            <-   CR LF ;

VCHAR           <-   [\\x21-\\x7E] ;

DQUOTE          <-   [\\x22] ;

HEXDIG          <-   DIGIT / 'A' / 'B' / 'C' / 'D' / 'E' / 'F' ;
